plugins {
    id 'com.android.library'
    id 'signing'
    id 'maven-publish'
}

ext {
    libName = 'Android BMP Encoder'
    libVendorName = 'Tianscar'

    libVersionCode = 1
    libGroupName = 'com.tianscar'
    libVersionName = '1.0.0'

    libSourceCompatibility = JavaVersion.VERSION_1_8
    libTargetCompatibility = JavaVersion.VERSION_1_8

    libAndroidNamespace = 'com.tianscar.androidbmpencoder'
    libAndroidCompileSdk = 33
    libAndroidMinSdk = 16
    libAndroidTargetSdk = 33
}

group libGroupName
version libVendorName

android {
    namespace libAndroidNamespace
    compileSdk libAndroidCompileSdk

    defaultConfig {
        minSdk libAndroidMinSdk
        targetSdk libAndroidTargetSdk
        versionCode libVersionCode
        versionName libVersionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility libSourceCompatibility
        targetCompatibility libTargetCompatibility
    }
}

dependencies {
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

android {
    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

publishing {

    publications {
        release(MavenPublication) {
            groupId = libGroupName
            artifactId = "${rootProject.name}-${project.name}"
            version = libVersionName
            afterEvaluate {
                from components.release
            }
            pom {
                name = libName
                description = 'The missing encoder to encode android.graphics.Bitmap to Windows Bitmap (*.bmp)'
                url = 'https://github.com/Tianscar/android-bmp-encoder'
                licenses {
                    license {
                        name = 'The MIT License'
                        url = 'https://spdx.org/licenses/MIT.html'
                    }
                }
                developers {
                    developer {
                        id = 'Tianscar'
                        name = 'Tiansword Carbonicacid'
                        email = 'tianscar@protonmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com/Tianscar/android-bmp-encoder.git'
                    developerConnection = 'scm:git:git@github.com/Tianscar/android-bmp-encoder.git'
                    url = 'https://github.com/Tianscar/android-bmp-encoder'
                }
            }
        }
    }
    repositories {
        maven {
            name = "OSSRH"
            if (project.version.toString().endsWith("-SNAPSHOT")) {
                url = "https://s01.oss.sonatype.org/content/repositories/snapshots"
            } else {
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            }
            credentials {
                username = findProperty("ossrhUsername") ?: System.getenv("OSSRH_USERNAME")
                password = findProperty("ossrhPassword") ?: System.getenv("OSSRH_PASSWORD")
            }
        }
    }
}

signing {
    def signingKey = findProperty("signingKey") ?: System.getenv("GPG_KEY")
    def signingPassword = findProperty("signingPassword") ?:System.getenv("GPG_PASSWORD")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign(publishing.publications.release)
}